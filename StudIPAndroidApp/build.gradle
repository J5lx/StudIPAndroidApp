/*******************************************************************************
 * Copyright (c) 2013 ELAN e.V.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 ******************************************************************************/
import java.text.SimpleDateFormat

def versionMajor = 0 //prod
def versionMinor = 8
def versionPatch = 2
def versionBuild = 1

def versionMajorBeta = 0 //beta
def versionMinorBeta = 8
def versionPatchBeta = 4
def versionBuildBeta = 2

final Console console = System.console();

buildscript {

    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }

}
apply plugin: 'android'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

dependencies {

    compile 'com.android.support:support-v4:18.+'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.2'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'
    compile 'oauth.signpost:oauth-signpost:1.2.1.2'
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.squareup.picasso:picasso:2.1.1'
    compile 'com.squareup.okhttp:okhttp:1.2.1'
    compile project(':libprojects:volley')
    compile project(':libprojects:SherlockNavigationDrawer:SherlockNavigationDrawer')
    compile project(':libprojects:encrypted-userprefs')

}

def buildTime() {

    def df = new SimpleDateFormat("yyyy-MM-dd HH:mm")
    df.setTimeZone(TimeZone.getTimeZone("CET"))
    return df.format(new Date())

}

android {

    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    // Building from console
    signingConfigs {
        release {

            if (System.getenv("KEYSTORE") != null)
                storeFile file(System.getenv("KEYSTORE"))
            else if (console != null)
                storeFile file(console.readLine("Enter keystore path: "))

            if (System.getenv("KEYSTORE_PASSWORD") != null)
                storePassword System.getenv("KEYSTORE_PASSWORD")
            else if (console != null)
                storePassword console.readLine("Enter keystore password: ")

            if (System.getenv("KEY_ALIAS"))
                keyAlias System.getenv("KEY_ALIAS")
            else if (console != null)
                keyAlias console.readLine("Enter alias key: ")

            if (System.getenv("KEY_PASSWORD") != null)
                keyPassword System.getenv("KEY_PASSWORD")
            else if (console != null)
                keyPassword console.readLine("Enter key password: ")

        }
    }



    defaultConfig {

        minSdkVersion 15
        targetSdkVersion 18

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    productFlavors {
        beta {
            minSdkVersion 10
            versionCode versionMajorBeta * 10000 + versionMinorBeta * 1000 + versionPatchBeta *
                    100 + versionBuildBeta
            versionName "${versionMajorBeta}.${versionMinorBeta}.${versionPatchBeta}-beta"
            buildConfig """\
          public static final String BUILD_TIME = "${buildTime()}";
          public static final String VERSION_NAME = \\"${versionName}\\";
          public static final String VERSION_CODE = \\"${versionCode}\\";
        """
        }

        prod {
            minSdkVersion 15
            versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            buildConfig """\
          public static final String BUILD_TIME = "${buildTime()}";
          public static final String VERSION_NAME = \\"${versionName}\\";
          public static final String VERSION_CODE = \\"${versionCode}\\";
        """
        }
    }

    buildTypes {

        debug {
            buildConfig "public static final boolean USE_CRASHLYTICS = false;"
            ext.enableCrashlytics = false;
            versionNameSuffix '-DEBUG'
            debuggable = true;
        }

        release {
            buildConfig "public static final boolean USE_CRASHLYTICS = true;"
            ext.enableCrashlytics = true;
            versionNameSuffix '-RELEASE'
            debuggable = false;
            signingConfig signingConfigs.release
            runProguard=true
            proguardFile 'proguard-project.txt'
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }

        applicationVariants.all { variant ->
            def file = variant.outputFile
            variant.outputFile = new File(file.parent, file.name.replace(".apk",
                    "-" + variant.productFlavors.versionName + ".apk"))
        }

    }

    sourceSets {

        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        release {
            manifest.srcFile 'AndroidManifestRelease.xml'
        }

        prod {
        }

        beta {
            res.srcDirs = ['resBeta']
        }

        debug {
        }

        instrumentTest.setRoot('tests')

    }

}